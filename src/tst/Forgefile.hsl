#
#                          Copyright (C) 2016, 2017 by Rafael Santiago
#
# This is a free software. You can redistribute it and/or modify under
# the terms of the GNU General Public License version 2.
#
#

include ~/toolsets/gcc/gcc-app.hsl
include ~/fsutil.hsl
include ../Utils.hsl

local var sources type list;
local var includes type list;
local var cflags type list;
local var libraries type list;
local var ldflags type list;
local var appname type string;

project CP8-tests : toolset "gcc-c-app" : $sources, $includes, $cflags, $libraries, $ldflags, $appname;

CP8-tests.prologue() {
    $sources.ls(".*\\.c$");
    $includes.add_item("../");
    $includes.add_item("lib/src");
    if (isdir("/usr/local/include")) {
        $includes.add_item("/usr/local/include");
    }

    $libraries.add_item("lib/src/lib");
    if (isdir("/usr/local/lib")) {
        $libraries.add_item("/usr/local/lib");
    }
    $libraries.add_item("../lib/acc/lib");

    $ldflags = cp8_arize();
    $ldflags.add_item("-laccacia");
    $ldflags.add_item("-lcutest");
    if (hefesto.sys.os_name() == "linux") {
        $ldflags.add_item("-ldl");
        $ldflags.add_item("-lpthread");
    } else if (hefesto.sys.os_name() == "freebsd") {
        $ldflags.add_item("-lexecinfo");
        $ldflags.add_item("-lpthread");
    }

    forge_cutest();

    $appname = "tests";
    if (hefesto.sys.os_name() == "windows") {
        $appname = $appname + ".exe";
    }
}

CP8-tests.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        runtests();
    }
}

local function forge_cutest() : result type none {
    ivk("cutest", "lib/src/", "Forgefile.hsl", "--obj-output-dir=obj --bin-output-dir=lib");
}

local function runtests() : result type none {
    var exit_code type int;
    $exit_code = hefesto.sys.run(hefesto.sys.make_path("bin", $appname));
    if ($exit_code != 0) {
        hefesto.sys.echo("~~~ THE TESTS HAVE ERROR(s)\n");
        hefesto.project.abort($exit_code);
    } else {
        hefesto.sys.echo("INFO: This code seems to be okay, in order to install it you can run \"hefesto --install\".\n");
    }
}

local function cp8_arize() : result type list { # ;-D
    var oldcwd type string;
    $oldcwd = hefesto.sys.pwd();

    if (hefesto.sys.cd("../obj")) {
        var objs type list;
        $objs.ls(".*\\.o$");

        var o type int;
        $o = 0;

        while ($o < $objs.count()) {
            var objpath type string;
            $objpath = $objs.item($o);
            if ($objpath.match(".*cp8\\.o$")) {
                $objs.del_index($o);
                break;
            }
            $o = $o + 1;
        }

        hefesto.sys.cd($oldcwd);
    }

    result $objs;
}
